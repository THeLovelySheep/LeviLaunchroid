plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    id 'org.tboox.gradle-xmake-plugin' version '1.2.3'
}

def getGitTag() {
    def tag = "unknown"
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        tag = stdout.toString().trim()
        if (tag.startsWith("v")) {
            tag = tag.substring(1)
        }
    } catch (ignored) {
    }
    return tag
}

static def getVersionCodeByTimestamp() {
    def baseDate = Date.parse("yyyy-MM-dd HH:mm:ss", "2025-01-01 00:00:00")
    def now = new Date()
    def diff = (now.time - baseDate.time) / 1000
    return diff.toInteger()
}

android {
    namespace 'org.levimc.launcher'
    compileSdk 36

    defaultConfig {
        applicationId "org.levimc.launcher"
        minSdk 24
        targetSdk 35
        versionCode getVersionCodeByTimestamp()
        versionName getGitTag()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }

        xmake {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    buildFeatures {
        viewBinding true
        prefab true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    externalNativeBuild {
        xmake {
            path "src/main/cpp/leviutils/xmake.lua"
            logLevel "verbose"
            buildMode "release"
        }

        ndkBuild {
            path "src/main/cpp/Android.mk"
        }

    }

    ndkVersion "27.0.12077973"

    packaging {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }
}

dependencies {
    implementation project(':preloader')
    implementation project(":minecraft")
    implementation project(":httpclient")
    implementation project(":microsoft:xal")
    implementation project(":microsoft:xbox")
    implementation project(":fmod")

    implementation libs.core.splashscreen
    implementation libs.apk.parser
    implementation libs.appcompat
    implementation libs.androidx.games.activity.v122
    implementation(libs.androidx.preference.ktx)
    implementation libs.conscrypt.android
    implementation libs.constraintlayout
    implementation libs.material
    implementation libs.gson
    implementation libs.okhttp3.okhttp
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}
